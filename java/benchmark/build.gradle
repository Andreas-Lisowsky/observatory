plugins {
    id 'java-library'
    id 'de.fuerstenau.buildconfig' version "${buildConfigVersion}"
    id 'com.palantir.git-version' version "${gitVersionVersion}"
}

group = 'de.hhu.bsinfo'
version = getProperty('projectVersion')

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

repositories {
    mavenCentral()

    maven {
        name = "GitHubPackages jDetector"
        url = "https://maven.pkg.github.com/hhu-bsinfo/jdetector"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getProperty("gpr.user")
            password = project.findProperty("gpr.token") ?: System.getProperty("gpr.token")
        }
    }
}

dependencies {
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "de.hhu.bsinfo:jdetector:${jDetectorVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
}

buildConfig {
    def gitVersion = versionDetails()

    appName = project.name
    version = project.version

    clsName = 'BuildConfig'
    packageName = 'de.hhu.bsinfo.observatory.generated'

    charset = 'UTF-8'

    buildConfigField 'String', 'BUILD_DATE', new Date().format('yyyy-MM-dd HH:mm:ss')
    buildConfigField 'String', 'GIT_COMMIT', gitVersion.gitHash
    buildConfigField 'String', 'GIT_BRANCH', gitVersion.branchName
}

// Workaround for 'Configuration <compile> not found', caused by de.fuerstenau.buildconfig with Gradle 7+
configurations {
    create("compile")
}
