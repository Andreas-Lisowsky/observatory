plugins {
    id 'java-library'
    id 'de.fuerstenau.buildconfig' version '1.1.8'
    id 'com.palantir.git-version' version '0.12.3'
}

group = 'de.hhu.bsinfo'
version = getProperty('projectVersion')

compileJava {
    sourceCompatibility = '8'
    targetCompatibility = '8'
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    maven {
        name = "GitHubPackages jDetector"
        url = "https://maven.pkg.github.com/hhu-bsinfo/jdetector"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getProperty("gpr.user")
            password = project.findProperty("gpr.token") ?: System.getProperty("gpr.token")
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    
    implementation 'org.slf4j:slf4j-api:1.7.31'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'de.hhu.bsinfo:jdetector:0.1.1'
}

buildConfig {
    def gitVersion = versionDetails()

    appName = project.name
    version = project.version

    clsName = 'BuildConfig'
    packageName = 'de.hhu.bsinfo.observatory.generated'

    charset = 'UTF-8'

    buildConfigField 'String', 'BUILD_DATE', new Date().format('yyyy-MM-dd HH:mm:ss')
    buildConfigField 'String', 'GIT_COMMIT', gitVersion.gitHash
    buildConfigField 'String', 'GIT_BRANCH', gitVersion.branchName
}
