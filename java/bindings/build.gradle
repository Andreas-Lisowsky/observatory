plugins {
    id 'java'
    id 'de.fuerstenau.buildconfig' version '1.1.8'
}

subprojects {
    apply plugin: 'application'
    apply plugin: 'de.fuerstenau.buildconfig'

    group = 'de.hhu.bsinfo'
    version = getProperty('projectVersion')

    repositories {
        mavenCentral()

        maven {
            name = "GitHubPackages jDetector"
            url = "https://maven.pkg.github.com/hhu-bsinfo/jdetector"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getProperty("gpr.user")
                password = project.findProperty("gpr.token") ?: System.getProperty("gpr.token")
            }
        }
    }

    dependencies {
        implementation project(':application')
        implementation project(':benchmark')

        implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    }

    mainClassName = 'de.hhu.bsinfo.observatory.app.Application'
    applicationName = 'observatory'

    applicationDistribution.from('../../../example/config/config.json') {
        into '/'
    }
}

// Workaround for 'Configuration <compile> not found', caused by de.fuerstenau.buildconfig with Gradle 7+
configurations {
    create("compile")
}